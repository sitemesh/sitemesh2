plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}

group = 'opensymphony'

repositories {
    mavenCentral()
}

java {
    toolchain { languageVersion = JavaLanguageVersion.of(17) }
    withSourcesJar()
    withJavadocJar()
}

configurations {
    lexer
}

sourceSets.main.java.srcDirs += ['build/generated-sources/jflex']

javadoc {
    failOnError = false
    exclude '**/Lexer.java'
}

jar {
    from('src/main/java') {
        include '**/*.xml'
    }
}

dependencies {
    implementation 'jakarta.servlet:jakarta.servlet-api:6.0.0'
    compileOnly 'jakarta.servlet.jsp:jakarta.servlet.jsp-api:3.1.1'

    testImplementation 'junit:junit:4.5'

    lexer 'de.jflex:jflex:1.3.5'
}

// Generate Lexer.java from JFlex before compiling Java
tasks.named('compileJava').configure {
    doFirst {
        ant.taskdef classname: 'JFlex.anttask.JFlexTask', name: 'jflex', classpath: configurations.lexer.asPath
        ant.jflex(
            file: 'src/main/java/com/opensymphony/module/sitemesh/html/tokenizer/lexer.flex',
            destdir: 'build/generated-sources/jflex'
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'Sitemesh'
                description = 'SiteMesh is a web-page layout and decoration framework and web- application integration framework to aid in creating large sites consisting of many pages for which a consistent look/feel, navigation and layout scheme is required.'
                url = 'https://github.com/sitemesh/sitemesh2'
                licenses {
                    license {
                        name =  'The Apache Software License, Version 1.1'
                        url = 'https://raw.githubusercontent.com/sitemesh/sitemesh2/master/LICENSE.txt'
                        distribution =  'repo'
                    }
                }
                developers {
                    developer {
                        id = 'joewalnes'
                        name = 'Joe Walnes'
                        email = 'joe dot walnes at gmail'
                    }
                    developer {
                        id = 'codeconsole'
                        name = 'Scott Murphy'
                    }
                }
                organization {
                    name = 'OpenSymphony'
                }
                scm {
                    connection = 'scm:git:git://github.com/sitemesh/sitemesh2.git'
                    developerConnection = 'scm:git:git@github.com:sitemesh/sitemesh2.git'
                    url = 'https://github.com/sitemesh/sitemesh2/'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/sitemesh/sitemesh2/issues'
                }
            }
        }
    }
}

signing {
    def key = findProperty('signing.key') as String
    def pass = findProperty('signing.password') as String
    if (key && pass) {
      useInMemoryPgpKeys(key, pass)
      sign publishing.publications
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
            username.set(providers.gradleProperty("sonatypeUsername"))
            password.set(providers.gradleProperty("sonatypePassword"))
        }
    }
}
