plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0-rc-1'
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}

group = 'opensymphony'
version = '2.5.1-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

configurations {
    lexer
}

sourceSets.main.java.srcDirs += ['build/generated-sources/jflex']

javadoc {
    failOnError = false
    exclude '**/Lexer.java'
}

java {
    withSourcesJar()
    withJavadocJar()
}

jar {
    metaInf {
        from('src/etc/tld/jsp1.2') {
            include '*.tld'
        }
    }
    from('src/main/java') {
        include '**/*.xml'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
    compileOnly 'javax.servlet.jsp:javax.servlet.jsp-api:2.3.3'
    compileOnly 'org.freemarker:freemarker:2.3.32'
    compileOnly 'velocity:velocity-dep:1.4'
    compileOnly 'velocity-tools:velocity-tools:1.4'
    compileOnly 'tapestry:tapestry:3.0.3'

    testImplementation group: 'junit', name: 'junit', version:'4.5'
    testImplementation 'javax.servlet:javax.servlet-api:4.0.1'
    testImplementation 'org.mockito:mockito-core:1.10.19'

    lexer 'de.jflex:jflex:1.3.5'
}

compileJava.doFirst {
    ant.taskdef classname: 'JFlex.anttask.JFlexTask', name: 'jflex', classpath: configurations.lexer.asPath
    ant.jflex(file: 'src/main/java/com/opensymphony/module/sitemesh/html/tokenizer/lexer.flex', destdir: 'build/generated-sources/jflex')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'Sitemesh'
                description = 'SiteMesh is a web-page layout and decoration framework and web- application integration framework to aid in creating large sites consisting of many pages for which a consistent look/feel, navigation and layout scheme is required.'
                url = 'https://github.com/sitemesh/sitemesh2'
                licenses {
                    license {
                        name =  'The Apache Software License, Version 1.1'
                        url = 'https://raw.githubusercontent.com/sitemesh/sitemesh2/master/LICENSE.txt'
                        distribution =  'repo'
                    }
                }
                developers {
                    developer {
                        id = 'joewalnes'
                        name = 'Joe Walnes'
                        email = 'joe dot walnes at gmail'
                    }
                    developer {
                        id = 'codeconsole'
                        name = 'Scott Murphy'
                    }
                }
                organization {
                    name = 'OpenSymphony'
                }
                scm {
                    connection = 'scm:git:git://github.com/sitemesh/sitemesh2.git'
                    developerConnection = 'scm:git:git@github.com:sitemesh/sitemesh2.git'
                    url = 'https://github.com/sitemesh/sitemesh2/'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/sitemesh/sitemesh2/issues'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}